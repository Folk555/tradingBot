plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'folk'
version = '1.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

bootJar {
    archiveBaseName.set('tradingBot')
    archiveVersion.set(version)
    archiveClassifier = null
    exclude 'hikari-test.properties'
    exclude 'folk/tradingbot/trader/configuration/TestDBConfiguration.class'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
//Задаем конкретную версию junit
ext['junit-jupiter.version'] = '5.11.4'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.postgresql:postgresql:42.7.5'
    implementation 'com.zaxxer:HikariCP:6.2.1'
    implementation 'ru.tinkoff.piapi:java-sdk-core:1.27'
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    modules {
        module("org.springframework.boot:spring-boot-starter-logging") {
            replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
        }
    }

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
    //VM Options для тестов запущенных через gradle
    jvmArgs '-Djava.library.path=C:\\Users\\folk\\Desktop\\stol\\td022025\\td\\tdlib\\bin'
    systemProperty 'spring.profiles.active', 'test'
}